#GoAndKickBallMapGoal
$AlignedToGoal
    YES --> $BallKickArea
        LEFT --> @Stop, @GoToRelativePosition + x:0.5 + y:0 + t:0
        RIGHT --> @Stop, @GoToRelativePosition + x:0.5 + y:0 + t:0
        FAR --> @GoToBall + target:map_goal
    NO -->  @GoToBall + target:map_goal

#GoAndKickBallDetectionGoal
$AlignedToGoal
    YES --> $BallKickArea
        LEFT --> @Stop, @GoToRelativePosition + x:0.5 + y:0 + t:0
        RIGHT --> @Stop, @GoToRelativePosition + x:0.5 + y:0 + t:0
        FAR --> @GoToBall + target:detection_goal
    NO --> @GoToBall + target:detection_goal

#GoAndKickBallAway
$BallKickArea
    LEFT --> @Stop, @GoToRelativePosition + x:0.5 + y:0 + t:0
    RIGHT --> @Stop, @GoToRelativePosition + x:0.5 + y:0 + t:0
    FAR --> @GoToBall + target:none

#NoLocalizationPlayerBehavior
$BallSeen
    YES --> $BallClose
        YES --> $GoalSeen
            YES --> #GoAndKickBallDetectionGoal
            NO --> #GoAndKickBallAway
        NO --> @GoToBall + target:none
    NO --> @Stop, @SearchBall

#NoLocalizationGoalieBehavior
$BallSeen
    YES --> $BallClose
        YES --> #GoAndKickBallAway
        NO --> @Stop, @LookAtBall
    NO --> @Stop, @LookAtBall

#Positioning
$Localization
    YES --> @GoToRolePosition, @LookAtFieldFeatures
    NO --> @GoToRelativePosition + x:2 + y:0 + t:0

#PositioningReady
$Localization
    YES --> @GoToRolePosition, @LookAtFieldFeatures
    NO --> @GoToRelativePosition + x:2 + y:0 + t:0, @StandAndWait

#GoalieBehavior
$Localization
    YES --> $JustUnpenalized
        YES --> #Positioning
        NO --> $BallSeen
            YES --> $BallDangerous
                YES --> #GoAndKickBallMapGoal
                NO --> @Stop, @LookAtBall
    NO --> $JustUnpenalized
        YES --> @GoToRelativePosition + x:2 + y:0 + t:0
        NO --> #NoLocalizationGoalieBehavior

#OffenseBehavior
$Localization
    YES --> $JustUnpenalized
        YES --> #Positioning
        NO --> $BallSeen
            YES --> #GoAndKickBallMapGoal
            NO --> @Stop, @SearchBall
    NO --> $JustUnpenalized
        YES --> @GoToRelativePosition + x:2 + y:0 + t:0
        NO --> #NoLocalizationPlayerBehavior

#DefenseBehavior
$JustUnpenalized
    YES --> #Positioning
    NO --> $Localization
        YES --> $BallSeen
            YES --> $BallInDefensiveArea
                YES --> #GoAndKickBallAway
                NO -->  $BallClose
                    YES --> #GoAndKickBallAway
                    NO --> @GoToDefensePosition
            NO --> @Stop, @SearchBall
        NO --> #NoLocalizationPlayerBehavior

#PenaltyBehavior
$BallSeen
    YES --> $BallKickArea
        RIGHT --> @Stop, @KickBallDynamic + type:penalty
        LEFT --> @Stop, @KickBallDynamic + type:penalty
        FAR --> $GoalSeen
            YES --> @GoToBall + target:detection_goal
            NO --> @GoToBall + target:none
    NO --> @SearchBallPenalty


-->BodyBehavior
$RoleDecider
    goalie --> $GameStateDecider
        Initial --> @LookAtBall, @StandAndWait
        Ready --> #PositioningReady
        Set --> @StandAndWait
        Playing --> #GoalieBehavior
        Finished --> @StandAndWait
        NOT_ALLOWED_TO_MOVE --> @LookAtBall, @StandAndWait
    offense --> $GameStateDecider
        Initial --> @LookAtBall, @StandAndWait
        Ready --> #PositioningReady
        Set --> @StandAndWait
        Playing --> #OffenseBehavior
        Finished --> @StandAndWait
        NOT_ALLOWED_TO_MOVE --> @LookAtBall, @StandAndWait
    defense --> $GameStateDecider
        Initial --> @LookAtBall, @StandAndWait
        Ready --> #PositioningReady
        Set --> @StandAndWait
        Playing --> #DefenseBehavior
        Finished --> @StandAndWait
        NOT_ALLOWED_TO_MOVE --> @LookAtBall, @StandAndWait
    penalty --> $GameStateDecider
        Initial --> @LookAtBallPenalty, @StandAndWait
        Ready --> #PositioningReady
        Set --> @StandAndWait
        Playing --> #PenaltyBehavior
        Finished --> @StandAndWait
        NOT_ALLOWED_TO_MOVE --> @LookAtBallPenalty, @StandAndWait
