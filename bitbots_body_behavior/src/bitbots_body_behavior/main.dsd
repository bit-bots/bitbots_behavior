#SearchBall
@ChangeAction + action:searching, @LookAtFieldFeatures, @StandAndWait + duration:4, @TurnAround

#DoNothing
@ChangeAction + action:waiting, @LookForward, @StandAndWait

#StandAndLook
@ChangeAction + action:waiting, @LookAtFieldFeatures, @StandAndWait

#GetWalkreadyAndLocalize
@ChangeAction + action:waiting + r:false, @LookForward + r:false, @PlayAnimationInit + r:false, @StandAndWait + duration:2 + r:false, @GetWalkready + r:false, @LookAtFieldFeatures + r:false, @StandAndWait + duration:2 + r:false, @StandAndWait


#GoAndKickBallMapGoal
$AvoidBall
    NO --> $BallClose + distance:%behavior/body/ball_reaproach_dist + angle:%behavior/body/ball_reapproach_angle
        YES --> $BallKickArea
            NEAR --> @ChangeAction + action:kicking, @Stop, @LookAtBall, @StandAndWait + duration:1.0, @LookForward + r:false, @KickBallDynamic, @LookAtFieldFeatures + r:false, @StandAndWait + duration:1 + r:false
            FAR --> @ChangeAction + action:going_to_ball, @LookAtBall, @GoToBall + target:map_goal
        NO --> @ChangeAction + action:going_to_ball + r:false, @LookAtFieldFeatures + r:false, @AvoidBallActive + r:false, @GoToBall + target:map_goal + blocking:false + distance:%behavior/body/ball_far_approach_dist
    YES --> $ReachedMovebaseGoalPosition + thres:%behavior/body/ball_far_approach_position_thresh
        YES --> @AvoidBallInactive
        NO --> @ChangeAction + action:going_to_ball, @LookAtFieldFeatures, @GoToBall + target:map_goal + distance:%behavior/body/ball_far_approach_dist

#GoAndKickBallDetectionGoal
$AlignedToMoveBaseGoal
    YES --> $BallKickArea
        NEAR --> @ChangeAction + action:kicking + r:false, @Stop + r:false, @LookForward + r:false, @KickBallDynamic + r:false
        FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal
    NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:detection_goal

#GoAndKickBallAway
$BallKickArea
    NEAR --> @ChangeAction + action:kicking + r:false, @Stop + r:false, @LookForward + r:false, @KickBallDynamic + r:false
    FAR --> @ChangeAction + action:going_to_ball, @GoToBall + target:close

#NoLocalizationPlayerBehavior
$BallSeen
    YES --> $BallClose
        YES --> $GoalSeen
            YES --> #GoAndKickBallDetectionGoal
            NO --> #GoAndKickBallAway
        NO --> @ChangeAction + action:going_to_ball, @GoToBall + target:close
    NO --> #SearchBall

#NoLocalizationGoalieBehavior
$BallSeen
    YES --> $BallClose
        YES --> #GoAndKickBallAway
        NO --> @ChangeAction + action:waiting, @Stop, @LookAtBall
    NO --> #SearchBall

#PositioningReady
$ConfigRole
    GOALIE --> $GoalScoreRecently
        YES --> @Stop + r:false, @PlayAnimationCheering + r:false, @GetWalkready + r:false
        NO --> $LocalizationAvailable
            YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @StandAndWait + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
            NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToRelativePosition + x:2 + y:0 + t:0, @StandAndWait
    ELSE --> $LocalizationAvailable
        YES --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @StandAndWait + duration:%behavior/body/ready_wait_time, @AvoidBallActive, @GoToRolePosition
        NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToRelativePosition + x:2 + y:0 + t:0, @StandAndWait

#ReorientationPause
$TimerRunning + name:reorientation
    YES --> $TimerEnded + name:reorientation
        YES --> @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration
        NO --> @ChangeAction + action:localizing, @LookAtFieldFeatures, @Stop
    NO --> @StartTimer + name:reorientation + duration:%behavior/body/reorientation_duration

#GoalieBehavior
$ClosestToBall
    YES --> $BallDangerous
        NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToBlockPosition, @LookAtFieldFeatures
        ELSE --> #GoAndKickBallMapGoal
    NO --> @ChangeAction + action:positioning, @GoToBlockPosition, @LookAtFieldFeatures

#StrikerRole
$GoalieHandlingBall
    YES --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassAcceptPosition //don't go too far back
    NO --> #GoAndKickBallMapGoal

#SupporterRole
$PassStarted
    YES --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassAcceptPosition
    NO --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToPassPreparePosition

#DefenderRole
@ChangeAction + action:positioning, @GoToDefensePosition

#GoalieRole
$LocalizationAvailable
    YES --> $LocalizationPrecision
        HIGH --> $TimerRunning + name:reorientation
            YES --> @EndTimer + name:reorientation + r:false, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration + r:false
            NO --> $CurrentScore
                BEHIND --> $LastPlayer
                    YES --> #StrikerRole
                    NO --> #GoalieBehavior
                ELSE --> #GoalieBehavior
        LOW --> $TimerEnded + name:reorientation_pause
            NO --> #NoLocalizationGoalieBehavior
            YES --> #ReorientationPause
    NO --> #NoLocalizationGoalieBehavior

#PenaltyShootoutBehavior
$IsPenaltyShootRobot
    NO --> #DoNothing
    YES -->$SecondaryStateTeamDecider
        OUR --> @StandAndWaitRandom + min:10 + max:30, @KickBallDynamic + r:true + type:penalty, @StandAndWait
        OTHER --> $BallDangerous
            LEFT --> @PlayAnimationGoalieFallLeft
            RIGHT --> @PlayAnimationGoalieFallRight
            CENTER --> @PlayAnimationGoalieFallCenter
            ELSE --> @LookAtBall


#Placing
$ConfigRole
    GOALIE --> @ChangeAction + action:positioning, @GoToBlockPosition, @LookAtFieldFeatures
    ELSE --> $BallSeen
        NO --> #SearchBall
        YES --> $SecondaryStateDecider
            CORNER_KICK --> $SecondaryStateTeamDecider
                OUR --> $RankToBallNoGoalie
                    FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:striker
                    SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:supporter
                    THIRD --> @ChangeAction + action:positioning, @AvoidBallActive, @GoToDefensePosition
                OTHER --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToCornerKickPosition + mode:others
            ELSE --> $SecondaryStateTeamDecider
                OUR --> $RankToBallNoGoalie
                    FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToBall + target:gradient_goal + distance:0.5
                    SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToPassPreparePosition
                    THIRD --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition
                OTHER --> $RankToBallNoGoalie
                    FIRST --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToBall + target:map_goal + distance:1
                    SECOND --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition + mode:freekick
                    THIRD --> @ChangeAction + action:positioning, @LookAtFieldFeatures, @AvoidBallActive, @GoToDefensePosition

-->BodyBehavior
$IsPenalized
    YES --> #DoNothing
    JUST_UNPENALIZED --> #GetWalkreadyAndLocalize
    NO --> $GameStateDecider
        INITIAL --> @ChangeAction + action:waiting + r:false, @LookForward + r:false, @PlayAnimationInit + r:false, @StandAndWait + duration:2, @GetWalkready + r:false, @LookAtFieldFeatures + r:false, @StandAndWait + duration:2 + r:false, @StandAndWait
        READY --> #PositioningReady
        SET --> $SecondaryStateDecider
            PENALTYSHOOT --> $SecondaryStateTeamDecider
                OUR -->  @DeactivateHCM + r:false, @LookForward + r:false, @PlayAnimationInit + r:false, @StandAndWait + duration:2 + r:false, @GetWalkready + r:false, @StandAndWait + duration:1 + r:false, @StandAndWait // we need to also see the goalie
                OTHER --> @DeactivateHCM + r:false, @LookForward + r:false, @PlayAnimationInit + r:false, @StandAndWait + duration:2 + r:false, @GetWalkready + r:false, @LookAtBall + r:false, @PlayAnimationGoalieArms + r:false, @StandAndWait // goalie only needs to care about the ball
            ELSE --> #StandAndLook
        FINISHED --> #DoNothing
        PLAYING --> $SecondaryStateDecider
            PENALTYSHOOT --> #PenaltyShootoutBehavior
            TIMEOUT --> #StandAndLook
            ELSE --> $SecondaryStateModeDecider
                ELSE --> #StandAndLook
                PLACING --> #Placing
            NORMAL --> $BallSeen
                NO --> $ConfigRole
                    GOALIE --> @GoToRolePosition
                    ELSE --> #SearchBall
                YES --> $KickOffTimeUp
                    NO --> #StandAndLook
                    YES --> $ConfigRole
                        GOALIE --> #GoalieRole
                        ELSE --> $LocalizationAvailable
                            YES --> $LocalizationPrecision
                                HIGH --> $TimerRunning + name:reorientation
                                    YES --> @EndTimer + name:reorientation + r:false, @StartTimer + name:reorientation_pause + duration:%behavior/body/reorientation_pause_duration + r:false
                                    NO --> $RankToBallNoGoalie
                                        FIRST --> #StrikerRole
                                        SECOND --> #SupporterRole
                                        THIRD --> #DefenderRole
                                LOW --> $TimerEnded + name:reorientation_pause
                                    NO --> #NoLocalizationPlayerBehavior
                                    YES --> #ReorientationPause
                            NO --> #NoLocalizationPlayerBehavior
